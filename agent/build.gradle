plugins {
  id("com.github.johnrengelman.shadow") version "7.0.0"
  id 'maven-publish'
  id 'signing'
}

apply from: "$rootDir/gradle/shadow.gradle"

project.archivesBaseName = 'solarwinds-apm-agent'

def relocatePackages = ext.relocatePackages

configurations {
  customShadow
}

dependencies {
  customShadow project(path: ":custom", configuration: "shadow")
  customShadow project(path: ":instrumentation", configuration: "shadow")
  implementation project(path: ":core-bootstrap")
  implementation project(path: ":solarwinds-otel-sdk")
  implementation "io.opentelemetry.javaagent:opentelemetry-javaagent:${versions.opentelemetryJavaagent}"
  implementation "com.appoptics.agent.java:core:${versions.appopticsCore}"
  implementation "com.appoptics.agent.java:metrics:${versions.appopticsMetrics}"
}

CopySpec isolateSpec() {
  return copySpec {
    configurations.customShadow.files.each {
      from(zipTree(it)) {
        into("inst")
        rename("(^.*)\\.class\$", "\$1.classdata")
      }
    }
  }
}

shadowJar {
  archiveClassifier.set(null)
}

tasks {
  shadowJar {
    dependsOn ':custom:shadowJar'
    dependsOn ':instrumentation:shadowJar'
    with isolateSpec()

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    mergeServiceFiles {
      include("inst/META-INF/services/*")
    }
    exclude("**/module-info.class")

    relocatePackages(it)

    manifest {
      attributes.put("Main-Class", "io.opentelemetry.javaagent.OpenTelemetryAgent")
      attributes.put("Agent-Class", "io.opentelemetry.javaagent.OpenTelemetryAgent")
      attributes.put("Premain-Class", "io.opentelemetry.javaagent.OpenTelemetryAgent")
      attributes.put("Can-Redefine-Classes", "true")
      attributes.put("Can-Retransform-Classes", "true")
      attributes.put("Implementation-Vendor", "SolarWinds Inc.")
      attributes.put("Implementation-Version", "${versions.agent}")
    }
  }

  assemble {
    dependsOn(shadowJar)
  }
}

task sourcesJar(type: Jar) {
  from('.') {
    include 'SOURCES_DOC_README'
  }
  archiveClassifier.set("sources")
}

task javadocJar(type: Jar) {
  from('.') {
    include 'SOURCES_DOC_README'
  }
  archiveClassifier.set("javadoc")
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = "${archivesBaseName}"
        description = "${archivesBaseName}"
        url = "www.solarwinds.com"
        scm {
          url = 'https://www.solarwinds.com'
        }
        developers {
          developer {
            id = 'APM'
            name = 'The APM Agent team'
          }
        }
        licenses {
          license {
            name = 'Librato Open License'
          }
        }
        groupId = 'io.github.appoptics'
        artifactId = "${archivesBaseName}"
        version = "${versions.agent}"
        from components.java
        artifact sourcesJar
        artifact javadocJar
      }
    }
  }
  repositories {
    maven {
      name = "OSSRH"
      url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = System.getenv("MAVEN_USERNAME")
        password = System.getenv("MAVEN_PASSWORD")
      }
    }
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/appoptics/solarwinds-apm-java"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}

signing {
  def signingKey = System.getenv("SIGNING_KEY")
  def signingPassword = System.getenv("SIGNING_PASSWORD")
  useInMemoryPgpKeys(signingKey, signingPassword)
  sign publishing.publications.mavenJava
}
