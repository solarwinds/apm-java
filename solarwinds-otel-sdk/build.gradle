/*
 * Â© SolarWinds Worldwide, LLC. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id("java")
  id("signing")
  id("maven-publish")
  id("com.github.johnrengelman.shadow")
}

apply from: "$rootDir/gradle/shadow.gradle"
project.archivesBaseName = 'solarwinds-otel-sdk'
def relocatePackages = ext.relocatePackages

def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

dependencies {
  compileOnly project(":bootstrap")
  compileOnly("io.opentelemetry:opentelemetry-sdk:${versions.opentelemetry}")

  compileOnly("io.opentelemetry:opentelemetry-api:${versions.opentelemetry}")
  compileOnly("io.opentelemetry:opentelemetry-context:${versions.opentelemetry}")
  compileOnly("io.opentelemetry.javaagent:opentelemetry-javaagent-bootstrap:${versions.opentelemetryJavaagentAlpha}")

  compileOnly("io.opentelemetry.instrumentation:opentelemetry-instrumentation-api:${versions.opentelemetryJavaagent}")

  testImplementation 'org.mockito:mockito-core:5.3.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.3.0'

  testImplementation 'org.mockito:mockito-inline:5.2.0'
  testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.2")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.9.2")
}

tasks.register('sourcesJar', Jar) {
  from('.') {
    include 'SOURCES_DOC_README'
  }
  archiveClassifier.set("sources")
}

tasks.register('javadocJar', Jar) {
  from('.') {
    include 'SOURCES_DOC_README'
  }
  archiveClassifier.set("javadoc")
}

shadowJar{
  archiveClassifier.set(null)
  relocatePackages(it)
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = "${archivesBaseName}"
        description = "${archivesBaseName}"
        url = "www.solarwinds.com"
        scm {
          url = 'https://www.solarwinds.com'
        }
        developers {
          developer {
            id = 'APM'
            name = 'The APM Library Team'
          }
        }
        licenses {
          license {
            name = 'Apache License, Version 2.0'
          }
        }

        groupId = 'io.github.appoptics'
        artifactId = "${archivesBaseName}"
        def sdkVersion = System.getenv("AGENT_VERSION") ?: "2.6.0"
        version = Boolean.parseBoolean(System.getenv("SNAPSHOT_BUILD")) ? "$sdkVersion-SNAPSHOT" : "${versions.agent}"

        from components.java
        artifact sourcesJar
        artifact javadocJar
      }
    }
    publishToMavenLocal
  }

  repositories {
    maven {
      name = "OSSRH"
      url = Boolean.parseBoolean(System.getenv("SNAPSHOT_BUILD")) ? snapshotsRepoUrl : releasesRepoUrl
      credentials {
        username = System.getenv("SONATYPE_USERNAME")
        password = System.getenv("SONATYPE_TOKEN")
      }
    }
  }
}

signing {
  setRequired {
    gradle.taskGraph.allTasks.any { (it.getClass() == PublishToMavenRepository.class) }
  }
  def signingKey = System.getenv("GPG_PRIVATE_KEY")
  def signingPassword = System.getenv("GPG_PRIVATE_KEY_PASSPHRASE")
  useInMemoryPgpKeys(signingKey, signingPassword)
  sign publishing.publications.mavenJava
}

test {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}

compileJava {
  options.release.set(8)
}

nexusPublishing {
  repositories {
    sonatype {
      password = System.getenv("SONATYPE_TOKEN")
      username = System.getenv("SONATYPE_USERNAME")

      nexusUrl = uri(releasesRepoUrl)
      snapshotRepositoryUrl = uri(snapshotsRepoUrl)
    }
  }
}