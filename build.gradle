group = "io.github.appoptics"

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            name = "sonatype"
            url = uri("https://oss.sonatype.org/content/repositories/snapshots")
        }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.14.0"
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        classpath "io.opentelemetry.instrumentation:gradle-plugins:1.31.0-alpha"
    }
}

subprojects {
  apply plugin: "java"
  apply plugin: "checkstyle"
  apply plugin: "com.diffplug.spotless"

    ext {
        versions = [
                opentelemetry         : "1.31.0",
                opentelemetryJavaagent: "1.31.0",
                bytebuddy             : "1.12.10",
                guava                 : "30.1-jre",
                appopticsCore         : "8.0.2",
                agent                 : "1.0.0", // the custom distro agent version
                autoservice           : "1.0.1",
        ]
        versions.appopticsMetrics = "${versions.appopticsCore}" // they share the same version now
        versions.opentelemetryAlpha = "${versions.opentelemetry}-alpha"
        versions.opentelemetrySemconv = "1.21.0-alpha"
        versions.opentelemetryJavaagentAlpha = "${versions.opentelemetryJavaagent}-alpha"

        deps = [
                bytebuddy  : "net.bytebuddy:byte-buddy-dep:${versions.bytebuddy}",
                autoservice: [
                        "com.google.auto.service:auto-service:${versions.autoservice}",
                        "com.google.auto.service:auto-service-annotations:${versions.autoservice}",
                ],
        ]
    }

    repositories {
        maven {
            url = uri("https://oss.sonatype.org/content/repositories/snapshots")
        }
        maven {
            url = uri("https://maven.pkg.github.com/solarwinds-cloud/maven-releases")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        testImplementation project(path: ":bootstrap")
        testImplementation 'org.mockito:mockito-core:5.3.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.3.0'
        testImplementation 'javax.annotation:javax.annotation-api:1.3.2'

        testImplementation 'org.mockito:mockito-inline:5.2.0'
        testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.2")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.9.2")

        testImplementation("io.opentelemetry:opentelemetry-sdk-extension-autoconfigure:${versions.opentelemetry}")
        testImplementation("io.opentelemetry.javaagent:opentelemetry-javaagent-extension-api:${versions.opentelemetryJavaagentAlpha}")
        testImplementation("io.opentelemetry.javaagent:opentelemetry-javaagent-tooling:${versions.opentelemetryJavaagentAlpha}")

        testImplementation("io.opentelemetry.instrumentation:opentelemetry-instrumentation-api:${versions.opentelemetryJavaagent}")
        testImplementation("io.opentelemetry.semconv:opentelemetry-semconv:${versions.opentelemetrySemconv}")
        testImplementation "com.appoptics.agent.java:core:${versions.appopticsCore}"
        testImplementation "com.appoptics.agent.java:metrics:${versions.appopticsMetrics}"
        testImplementation 'org.junit-pioneer:junit-pioneer:2.1.0'

    }

    checkstyleMain {
        configFile = file("$rootDir/checkstyle.xml")
    }

    checkstyleTest.enabled = false

    checkstyleTest {
        configFile = file("$rootDir/checkstyle.xml")
    }

    tasks {
        test {
            useJUnitPlatform()
        }

        compileJava {
            options.release.set(8)
        }
    }

    javadoc {
        source = sourceSets.main.allJava
    }

  spotless {
    format "misc", {
      target ".gitattributes", ".gitignore"
      trimTrailingWhitespace()
      endWithNewline()
    }

    java {
      target "src/*/java/**/*.java"
      removeUnusedImports()
      googleJavaFormat()
      formatAnnotations()
    }
  }
}
